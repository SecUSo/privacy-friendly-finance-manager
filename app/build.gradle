apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdk 34

    namespace 'org.secuso.privacyfriendlyfinance'
    defaultConfig {
        applicationId 'org.secuso.privacyfriendlyfinancemanager'
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 8
        versionName '2.3.0'
    }
    buildFeatures {
        //noinspection DataBindingWithoutKapt
        dataBinding true
        buildConfig true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        checkReleaseBuilds true
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    lint {
        lintConfig = file("lint.xml")
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    kotlin {
        jvmToolchain(17)
    }
    android.applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            def appName = "pfa-2048"
            outputFileName = appName + "-${variant.name}-v${variant.versionName}.apk"
        }
    }
}

dependencies {
    implementation project(':backup-api')

    testImplementation 'junit:junit:4.13.2'

    // for unittests replaces net.danlew:android.joda that causes 'Resource not found: "org/joda/time/tz/data/ZoneInfoMap"'
    // see https://github.com/dlew/joda-time-android/issues/37
    testImplementation 'joda-time:joda-time:2.9.4.2'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'net.danlew:android.joda:2.10.12.2'

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    implementation 'net.zetetic:android-database-sqlcipher:4.5.0'
    implementation 'androidx.room:room-runtime:2.6.0'
    annotationProcessor 'androidx.room:room-compiler:2.6.0'

    def work_version = '2.7.1'
    implementation "androidx.work:work-runtime:$work_version"
    androidTestImplementation "androidx.work:work-testing:$work_version"

    implementation 'com.github.QuadFlask:colorpicker:0.0.15'

    // https://opencsv.sourceforge.net/ License: Appache2 https://opencsv.sourceforge.net/licenses.html
    implementation 'com.opencsv:opencsv:5.7.1'

    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
}
